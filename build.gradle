plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'war'
	id 'idea'
	id 'checkstyle'
	id 'jacoco'
	id 'org.sonarqube' version '3.0'
}

group = 'com.softserveinc'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

bootRun {
	main = "com.softserveinc.dokazovi"
}

war {
	enabled = true
}

checkstyleMain {
	configFile = file("${rootDir}/checkstyle.xml")
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	def mapstruct = "1.3.1.Final"

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.flywaydb:flyway-core'
	implementation "org.mapstruct:mapstruct:${mapstruct}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstruct}"
	testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstruct}"
	compileOnly "org.projectlombok:lombok"
	annotationProcessor "org.projectlombok:lombok"

	implementation 'io.springfox:springfox-boot-starter:3.0.0'
	runtimeOnly 'org.postgresql:postgresql'

	testImplementation "com.h2database:h2"
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

jacoco {
	toolVersion = "0.8.5"
}

jacocoTestReport {
	dependsOn test
	reports {
		html.enabled true
		xml.enabled true
		csv.enabled false
		html.destination file("${buildDir}/jacoco/reports/html")
		xml.destination file("${buildDir}/jacoco/reports/jacocoTestReport.xml")

	}
}

sonarqube {
	tasks['sonarqube'].dependsOn jacocoTestReport
	properties {
		property 'sonar.host.url', 'http://localhost:9000'
		property "sonar.sources", "src/main"
		property "sonar.tests", "src/test"
		property "sonar.jacoco.coverage.reportPaths", "$buildDir/jacoco/reports/jacocoTestReport.xml"
		property "sonar.junit.reportsPaths", "$buildDir/test-results/"
	}
}